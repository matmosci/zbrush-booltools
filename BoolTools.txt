/*
    BoolTools for Zbrush
    https://github.com/matmosci/zbrush-booltools
    Author: matmosci
*/

[VarDef, BtnSize, [IGet,"Preferences:Interface:Buttons Size"]]
[VarDef, SubToolBtnSize, [Val, BtnSize*3.615]]
[VarDef, ActiveSubToolPath, ""]

[RoutineDef, ActiveSubToolPathSet,
    [VarSet, ActiveSubToolPath, [StrMerge,Tool:SubTool:,[StrExtract,[IGetTitle,Tool:ItemInfo],0,[StrLength,[IGetTitle,Tool:ItemInfo]]-2]]]
]
[RoutineDef, ActiveSubToolBoolStart,
    [RoutineCall, ActiveSubToolPathSet]
    [IClick, ActiveSubToolPath, [Val, SubToolBtnSize-115], 8]
]
[RoutineDef, ActiveSubToolBoolUnion,
    [RoutineCall, ActiveSubToolPathSet]
    [IClick, ActiveSubToolPath, [Val, SubToolBtnSize-95], 8]
]
[RoutineDef, ActiveSubToolBoolCut,
    [RoutineCall, ActiveSubToolPathSet]
    [IClick, ActiveSubToolPath, [Val, SubToolBtnSize-75], 8]
]
[RoutineDef, ActiveSubToolBoolIntersect,
    [RoutineCall, ActiveSubToolPathSet]
    [IClick, ActiveSubToolPath, [Val, SubToolBtnSize-55], 8]
]

[RoutineDef, XBoolInset,
    [IPress,Tool:SubTool:SelectUp]
    [RoutineCall,ActiveSubToolBoolStart]
    [IPress,Tool:SubTool:Duplicate]
    [RoutineCall,ActiveSubToolBoolStart]
    [IPress,Tool:SubTool:SelectDown]
    [IPress,Tool:SubTool:MoveUp]
    [RoutineCall,ActiveSubToolBoolCut]
    [IPress,Tool:SubTool:Duplicate]
    [IPress,Tool:SubTool:MoveDown]
    [RoutineCall,ActiveSubToolBoolIntersect]
    [IPress,Tool:SubTool:SelectUp]
]

[ISubPalette, "Zplugin:BoolTools"]

[IButton, "Zplugin:BoolTools:XInset", "Select Cutter SubTool (below) intersecting Primary SubTool (above) and press XInset to create an inset within the intersecting area. Also make sure the starting group is NOT set - it does it automatically.",
    [If, 0 == [SubToolGetActiveIndex],
        [Note, "Cutter SubTool must be selected and placed below Primary SubTool."],
        [RoutineCall, XBoolInset]
    ],
    0,
    [Val, BtnSize*2]
]

[IButton, "Zplugin:BoolTools:ToggleStart", "Toggle active SubTool boolean Start group.",
    [RoutineCall, ActiveSubToolBoolStart],
    0,
    [Val, BtnSize*2]
]
[IButton, "Zplugin:BoolTools:SetUnion", "Set active SubTool boolean Union.",
    [RoutineCall, ActiveSubToolBoolUnion],
    0,
    [Val, BtnSize*4/3]
]
[IButton, "Zplugin:BoolTools:SetCut", "Set active SubTool boolean Cut.",
    [RoutineCall, ActiveSubToolBoolCut],
    0,
    [Val, BtnSize*4/3]
]
[IButton, "Zplugin:BoolTools:SetIntersect", "Set active SubTool boolean Intersect.",
    [RoutineCall, ActiveSubToolBoolIntersect],
    0,
    [Val, BtnSize*4/3]
]

/*
[IButton, "Zplugin:BoolTools:Fold", "New Folder",
    [IPress, "Tool:SubTool:New Folder"],
    0,
    [Val, BtnSize*1]
]
[IButton, "Zplugin:BoolTools:Dupli", "Duplicate",
    [IPress, "Tool:SubTool:Duplicate"],
    0,
    [Val, BtnSize*1]
]
[IButton, "Zplugin:BoolTools:Up", "Select Up",
    [IPress, "Tool:SubTool:SelectUp"],
    0,
    [Val, BtnSize*1]
]
[IButton, "Zplugin:BoolTools:Down", "Select Down",
    [IPress, "Tool:SubTool:SelectDown"],
    0,
    [Val, BtnSize*1]
]
[IButton, "Zplugin:BoolTools:MvUp", "Move Up",
    [IPress, "Tool:SubTool:MoveUp"],
    0,
    [Val, BtnSize*1]
]
[IButton, "Zplugin:BoolTools:MvDown", "Move Down",
    [IPress, "Tool:SubTool:MoveDown"],
    0,
    [Val, BtnSize*1]
]
*/

/* DEBUG START */
[ISubPalette, "Zplugin:BoolTools:Debug"]
[IButton, "Zplugin:BoolTools:Debug:STIndex", "Show active SubTool index.",
    [Note, [SubToolGetActiveIndex]],
    0,
    [Val, BtnSize*2]
]
[IButton, "Zplugin:BoolTools:Debug:STPath", "Show active SubTool path.",
    [RoutineCall, ActiveSubToolPathSet]
    [Note, ActiveSubToolPath],
    0,
    [Val, BtnSize*2]
]
[IButton, "Zplugin:BoolTools:Debug:STBtnRealWidth", "Check SubTool button real width to compare with calculated width.",
    [RoutineCall, ActiveSubToolPathSet]
    [Note, [IWidth,ActiveSubToolPath]],
    0,
    [Val, BtnSize*4]
]
[IButton, "Zplugin:BoolTools:Debug:STBtnCalcWidth", "Check SubTool button calculated width to compare with real width.",
    [Note, SubToolBtnSize],
    0,
    [Val, BtnSize*4]
]
/* DEBUG END */
